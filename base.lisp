(in-package #:ski)

(defclass term () ()
  (:documentation "The base class for any kind of term."))

(defun term-p (object)
  "Return true if OBJECT is a TERM, and NIL otherwise."
  (typep object 'term))

(defgeneric term-equal (term1 term2)
  (:documentation "Return true if the two terms are exactly equal, and NIL otherwise."))

(defgeneric print-term (term &optional stream)
  (:documentation "Print a parsable representation of the TERM to the specified STREAM."))

(defclass variable ()
  ((name :initarg :name :accessor name))
  (:documentation "The base class for a variable."))

(defmethod initialize-instance :after ((variable variable) &key)
  (with-slots (name) variable
    (setf name (etypecase name
                 (list name)
                 (string (coerce name 'list))
                 (character (list name))
                 (symbol (coerce (string-downcase (symbol-name name)) 'list))))))

(defun variable-p (object)
  "Return true if OBJECT is a VARIABLE, and NIL otherwise."
  (typep object 'variable))

(defmethod print-object ((object variable) stream)
  (with-slots (name) object
    (print-unreadable-object (object stream :type t :identity t)
      (princ name stream))))

(defmethod print-term ((term variable) &optional (stream *standard-output*))
  (with-accessors ((name name)) term
    (if (rest name)
        (format stream "{狺钺礤痱轭ㄦ轵篝钺礤篝蝈犴┅翦蝽ㄤ彐躅鲠蜷徕戾倔礅镬鲠蜷徕戾⒁弭躜簌礅镬翳狒栳翳筢礤钺礤狍至疑谅膛ㄩ铘弪篝蜷铉躔汜箦ㄣ镥蜚钺礤鲠蜷徕戾篝蜷铉┅┅ㄤ彐沆狍鲠蜷徕戾钺礤珏铄蜥麸īè篝狒洪铋翩矧扉篝＼岍┅ê滹沲礤铘狒轱⑶孱弪狒矧镡赍泗麸狨麸磲糸汜祆泸遽翦鲠蜷徕戾钺礤螽┅ㄤ彐躅磲脲鲠蜷徕戾钺礤珏铄蜥麸ī⒚镱篝蝓泗犷蝈趱蝾至疑谅膛瘟团桥闻伊韵耶磲脲轭篝犷沐鲠蜷徕戾钺礤珏铄蜥麸颟ㄤ彐躅珏铄蜥翦钺礤ㄧ孱弪狒矧⒁弭躜翳铄鲠蜷徕戾钺礤骝镯桥闻伊韵耶鏖翳箪雉篝狒濠珏铄蜥麸痱镧ㄣ镳扉篝篝狒濠ㄩㄥ鲥蝙灬礅溽ㄣㄣ栳蚪＼┅篝狒濠箦翩篝狒磲脲扉篝ū戾铉翳篝狒濠洪铋糸犰屐屙孱＼岍戾è痫ū矧痫箝糸镱＼篝狒濠戾铉翳篝狒濠┅┅箦翩铘痫篝狒濠ㄣ镤瀛汨狎ūㄣ栳颦泔溴铘痫篝狒濠┅┅┅┅ㄤ彐珏铄蜷筢礤鲠蜷徕戾鲠蜷徕戾鲠蜷徕戾博ê滹沲礤铘狒轱⒁弭躜趄蹂殒至疑谅膛犷至疑谅膛栳鲥翳筢礤钺礤犷紊雉桢蝼轶瀹┅ㄤ彐礤翳镤筢礤鲠蜷徕戾è鲠蜷徕戾鲠蜷徕戾鲠蜷徕戾鲠蜷徕戾┅ㄥ聃犰钺礤鲠蜷徕戾暴钺礤鲠蜷徕戾博┅ㄤ彐沆狍狃痨殂狒轱īè戾骠洪铋翎蜱红彐横沣弩箫戾骠蜷玷洪铋翎蜱候殓梏横沣弩箫蜷玷舂ê滹沲礤铘狒轱⒃桢忉箦沆狍骘犷狃痨殂狒轱町┅ㄤ彐躅狃痨殂狒轱瞽镡赍泗⒁弭躜趄蹂殒下逝迷轶犷列刑擅猎上维犷紊雉桢蝼轶瀹豉疱镡赍泗п痧扉汜糸镱┅ㄤ彐礤翳镤痱轭舡镡赍泗è镡赍泗狃痨殂狒轱瞟篝蝈犴鏖翳箪雉戾骠蜷玷舂镡赍泗痱轭舡躅蝈徜徕戾镡赍泗镡赍泗篝蝈犴呼疱洪溴铘轸舂ㄦ矧磲篝蝈犴岍戾骠蜷玷舂┅ㄤ彐珏铄蜷镢沲蝮骝邋鲠蜷徕戾翦蝽ê滹沲礤铘狒轱⒁弭躜趄蹂殒至疑谅膛轶骝邋轭耘彝犷紊雉桢蝼轶瀹┅ㄤ彐礤翳镤镢沲蝮骝邋è鲠蜷徕戾鲠蜷徕戾翦蝽鲠蜷徕戾┅筢礤鲠蜷徕戾鲠蜷徕戾翦蝽┅ㄤ彐礤翳镤镢沲蝮骝邋è鲠蜷徕戾鲠蜷徕戾翦蝽狃痨殂狒轱瞟鏖翳徙沐篌矧è戾骠戾骠蜷玷蜷玷舂翦蝽矧镢沲蝮骝邋鲠蜷徕戾戾骠镢沲蝮骝邋鲠蜷徕戾蜷玷舂┅ㄤ彐珏铄蜷蝈漉沐翦蝽翦蝽ê滹沲礤铘狒轱⒁弭躜翳铒蝽犰骘蝽镦耘彝┅